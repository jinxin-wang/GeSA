## load json configuration file
print("[message] Loading configuration file")

configfile: "workflow/config/config.json"

include: "rules/init_pipeline.smk"

include: "rules/data/utils/gz.smk"
    
if config["do_sam2fastq"]:
    include: "rules/Sam2fq/sam2fq.smk"

######## clean fastq files with fastp ########
if config["do_fastp"]:
    include: "rules/Fastp/fastp.smk"

######## Fast QC ########
if config["do_qc"]:
    include: "rules/Fastqc/fastqc.smk"

######## align single-end or paired-end DNA samples with bwa ########
if config["do_bam"]:
    include: "rules/Align/bwa.smk"

    ######## [GATK - BQSR] Base Quality Score Recalibration ########
    ## https://gatk.broadinstitute.org/hc/en-us/articles/360035890531-Base-Quality-Score-Recalibration-BQSR-
    ## remove duplicated readswith, then do BQSR
    include: "rules/BQSR/BQSR_pass1.smk"

include: "rules/BQSR/BQSR_pass2.smk"

######## Facets ########
if config["do_cnvfacet"]:
    include: "rules/Facets/cnvfacets.smk"
    
if TUMOR_ONLY == False or NORMAL_ONLY == False : 
    include: "rules/Facets/facets.smk"

######## HaplotypeCaller and Annovar ########

## Call germline SNPs and indels via local re-assembly of haplotypes
if config["do_haplotype"]:
    include: "rules/HaplotypeCaller/haplotypeCaller.smk"

## Annovar on Haplotype caller, only to normal samples
if config["do_annovar"]:
    include: "rules/Annovar/annovar.smk"

if NORMAL_ONLY == False : 
    ######## Mapping QC ########
    
    if config["do_mqc"]:
        ## check mapping stats, with GATK CollectHsMetrics
        include: "rules/HsMetrics/hsMetrics.smk"
    
        ## check mapping metrics with samtools flagstat
        include: "rules/Flagstat/flagstat.smk"
    
    ######## Annotate on Mutect2 ########
    
    ## common rules for mutect2
    include: "rules/Mutect2/Common/collectSeqAM.smk"
    include: "rules/Mutect2/Common/estiContamination.smk"
    
    ## Tumor vs Normal mode
    if TUMOR_ONLY == False:
        if config["do_mutect2"] == True :
            include: "rules/Mutect2/Std/mutect2.smk"
        
        if config["samples"] == "human" and config["do_oncotator"] == True :
            include: "rules/Mutect2/Std/Split/oncotator.smk"
    
            if config["seq_type"] == "WGS":
                include: "rules/Mutect2/Std/Exom/oncotator.smk"
    
        elif config["samples"] == "mouse":
            include: "rules/Mutect2/Std/Annovar/annovar.smk"
        
        ## Tumor vs Normal with panel of normals (PON)
        if config["do_mutect2"] == True and config["mode"][-1] == "p":
            include: "rules/Mutect2/Pon/mutect2.smk"
    
        if config["samples"] == "human" and config["do_oncotator"] == True :
            include: "rules/Mutect2/Pon/Split/oncotator.smk"
            
            if config["seq_type"] == "WGS" and config["do_oncotator"] == True :
                include: "rules/Mutect2/Pon/Exom/oncotator.smk"
    
    ## Tumor Only mode
    elif TUMOR_ONLY == True:
        if config["do_mutect2"] == True :
            include: "rules/Mutect2/TumorOnly/mutect2.smk"
    
        if config["samples"] == "human" and config["do_oncotator"] == True :
            include: "rules/Mutect2/TumorOnly/Split/oncotator.smk"
    
            if config["seq_type"] == "WGS" and config["do_oncotator"] == True :
                include: "rules/Mutect2/TumorOnly/Exom/oncotator.smk"
        
        elif config["samples"] == "mouse":
            include: "rules/Mutect2/TumorOnly/Annovar/annovar.smk"
        
        ## Tumor Only with panel of normals (PON)
        if config["do_mutect2"] == True and config["mode"][-1] == "p":
            include: "rules/Mutect2/TumorOnlyPon/mutect2.smk"
    
        if config["samples"] == "human" and config["do_oncotator"] == True :
            include: "rules/Mutect2/TumorOnlyPon/Split/oncotator.smk"
            
            if config["seq_type"] == "WGS" and config["do_oncotator"] == True :
                include: "rules/Mutect2/TumorOnlyPon/Exom/oncotator.smk"
    
## tag in snakemake log, please don't modify
onsuccess:
    print("[TAG] DNA routine analysis complete")

rule all:
    input:
        expand('DNA_samples/{sample}_{read}.fastq.gz', sample = TSAMPLE + NSAMPLE if config["do_sam2fastq"] else [], read = [1,2] if config["do_sam2fastq"] else []),
        # FASTQ = expand("DNA_samples/{sample}_{reads}.fastq.gz", sample=SAMPLES, reads=['1','2'] config["paired"] == True else ['0'])
        expand('fastq_QC_raw/{sample}_{reads}_fastqc.html', sample = SAMPLES if config["do_qc"] else [], reads=['1','2'] if config["paired"] else ['0']),
        expand('fastq_QC_clean/{sample}_{reads}_fastqc.html', sample = SAMPLES if config["do_qc"] else [], reads=['1','2'] if config["paired"] else ['0']),
        expand('bam/{sample}.nodup.recal.bam.bai' if config["remove_duplicates"] == True else 'bam/{sample}.recal.bam.bai', sample = TSAMPLE + NSAMPLE if config["do_bam"] else []),
        expand("haplotype_caller_filtered/{sample}_germline_variants_filtered.vcf.gz", sample = NSAMPLE if config["do_haplotype"] == True else []),
        expand("annovar/{sample}.%s_multianno.txt.gz"%config["annovar"][config["samples"]]["ref"], sample = NSAMPLE if config["samples"] == "human" and config["do_annovar"] == True else []),
        expand('mapping_QC/HsMetrics/{sample}_HsMetrics.tsv', sample = TSAMPLE + NSAMPLE if config["do_mqc"] == True else []),
        expand("mapping_QC/flagstat/{sample}_flagstat.txt",   sample = TSAMPLE + NSAMPLE if config["do_mqc"] == True else []),
        expand(ANNOVAR if config["do_annovar"] == True else []),
        expand(FACETS if config["do_facet"] == True else []),
        expand(CNV_FACETS if config["do_cnvfacet"] == True else []),
        expand(MUTECT2 if config["do_mutect2"] == True else []),
        expand(ONCOTATOR_MAF if config["do_oncotator"] == True else []),
        expand(ONCOTATOR_EXOM_MAF if config["do_oncotator"] == True else []),
        expand(ONCOTATOR_COSMIC if config["do_oncotator"] == True else []),
        expand(ONCOTATOR_EXOM_COSMIC if config["do_oncotator"] == True else []),
        
